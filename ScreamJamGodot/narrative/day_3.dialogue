# import "res://narrative/day_2.dialogue" as snippets

~ day_3_house_am
if sanity == 100:
	Scott wakes up and sees the blue sky, stocked with clouds.
	do emit("turnOffAlarm") 
	The weather seems to be getting nicer each day.
	do emit("catMeow")
	Scott: Hey buddy!
	He pets Gambit.
	Scott: Having a nice morning?
	He pays a little more attention to his hair today.
	Scott: I think it's time for a haircut.
	There’s almost no use. It doesn’t look great, but he’s done the best he can with it.
else:
	Another morning of grey clouds. Scott sighs.
	do emit("turnOffAlarm")
	Scott: It's cool. I got this.
	He gets up and takes a shower, and scrubs his skin. 

Gambit needs to be fed, of course. Scott gets dressed and heads to work.

do emit("showCoffee")
As usual, he stops in at the cafe on his way to the train. There's a few people already working at tables, but no one in line.

Barista: Hi, Scott! Black coffee with one cream?
do emit("showMouseSignal")
- Scott: Yep, the usual please.
- Scott: What's the seasonal latte?
	do emit("hideMouseSignal")
	Barista: Well, it's made with oatmilk and a house mint syrup we make. I really like it.
	do emit("showMouseSignal")
	- Scott: Sure, I'll try that.
		do emit("hideMouseSignal")
		do sanity -= 10
		[i](Almost instantly, my stomach hurts. I feel woozy.)[/i]
		The barista doesn't notice his face go a little pale.
		Barista: Of course! This will take a little longer than usual, but there's not a line so it'll be up quick.
		He waits for a few minutes, at the counter, feeling awkward.
		She runs back and forth, prepping ingredients. 
		While it's not as quick as his usual order, he's out the door, with a wave.
		He does have to run to make sure he doesn't miss his normal train.
	- Scott: Eh. The usual is fine.
		do emit("hideMouseSignal")
		Barista: You got it.
		She turns around and almost instantly turns back, holding his normal coffee.
		He smiles and waves, and walks into the clear day.
do emit("toTrainAM")
=> END!
# lowest possible sanity here is 80

~ day_3_train_am
if sanity == 100:
	The train is a little more crowded than it has been, but it's probably due to the weather clearing up.
else:
	The mostly empty train is a welcome sight. He takes a seat. 
A man comes on and starts asking him for money.
Stranger: Do you have any money?
Scott: No, sorry. I don't carry cash.
The stranger walks away, calmly, not a threat at all.

do emit("toOffice")
=> END!

~ day_3_office
if sanity == 100:
	The office seems busier than normal, and everyone is in good spirits.
	The other people on his team are having meetings about training material for new employees.
	Scott: I'm so glad I don't have to do those presentations.
	Scott: I love talking in front of a crowd, but not about insurance benefits.
	Alexis: Next time, I'm going to make you help me. 
	Scott: Nope, not in my job description!
else:
	Scott notices a few of his colleagues are out, having said they'd work from home today.
	The amount of work he has to do is light, and he finds himself reading the news instead.
do emit("jumpScareSound")
[wait=1] ! [wait=1]
Scott feels a hand clasp his shoulder. He looks up and sees his boss, Dylan, smiling down at him.
Dylan: Hey, man. What's up?
do emit("showMouseSignal")
- Scott: Not much. Just working on next pay period.
- Scott: I don't really have a lot to do, but--
do emit("hideMouseSignal")
Dylan: Remember the other day when you said we should go get lunch together?
do emit("showMouseSignal")
- Scott: No?
- Scott: Sure.
do emit("hideMouseSignal")
Dylan: Can you go today?
do emit("showMouseSignal")
- Scott: I meant we'd both order from the app and then sit together.
	do emit("hideMouseSignal")
	Dylan: So you can order that Green Seeds salad for the umpteenth time?
	Dylan: I thought we'd go outside.
	if sanity == 100:
		Dylan: It's such a nice day. Come on, we should go.
		Dylan is insistent, emphatic. 
		=> day_3_lunch_dec
- Scott: No, I'm way too busy. 
	do emit("hideMouseSignal")
	Dylan: Scott, I'm your boss. I know you're not.
	=> day_3_lunch_dec
	
~ day_3_lunch_dec
do emit("showMouseSignal")
- Scott: Okay, you got me.
	do emit("hideMouseSignal")
	do sanity -= 10
	Scott feels something twist inside him. He gasps.
	Dylan doesn't notice.
	Dylan: Sweet. I want to take you to this ramen place that just opened up.
	They eat. Dylan pays, which is nice. 
	The walk back from the restaurant seems gloomy.
	Dylan: You think it's going to rain?
- Scott: Sorry, I already ordered from the app.
	do emit("hideMouseSignal")
	Dylan: Bummer, man. Ah well. We can go another day.
	Scott eats his salad for lunch the same way he always does\: he pours the dressing in, puts the lid back on, and shakes it.
	
The rest of the day is slow and brutal as Scott runs out of things to work on.
# lowest possible sanity here is 70
do time = TIMEOFDAY.NIGHT
do emit("toTrainPM")
=> END!


~ day_3_train_pm

if sanity == 100:
	It's getting sunnier out. 
	Scott stands again in the crowded train. The afternoon sun falls on his face as he rides the train home.
elif sanity >=80:
	It's a little gloomy.
	Few seats are available, but he gets one facing forward.
else:
	The day is unusually dark. The walk to the train seemed weird. 
	Scott is unusually sweaty.
	The empty train seems weird in the middle of rush hour, but Scott is glad to sit and ride home in silence.

do emit("toHousePM")
=> END!

~ day_3_house_pm

Scott: Chicken teriyaki... again. But I chose this life.
Gambit meows again.
Scott: I assume you're okay with it.
if sanity <=80:
	Scott still feels strangely sweaty, but tries to brush it off.


do emit("showCall")
=> END!

~ deny_call_mom
if missed_calls >= 0:
	do sanity -= 10
	do missed_calls += 1
	Something in the back of his neck twinges.
	Scott: I'll call her back later.
	He reads the transcript of the voicemail ("Hi, Scott! Just wanted to check in! Call me back! Love you, bye!")..
elif missed_calls == 1:
	do sanity -= 10
	do missed_calls += 1
	The sides of his face feel flush and hot, like something is tearing inside him.
	Scott: I'll get back to her, eventually.
	He reads the transcript of the voicemail ("Hi, Scott! Just wanted to check in! It's been a few days. So... call me back! Love you, bye!"). 
=> post_phone_call_3

~ pick_up_call_mom
Scott: Hi, mom.
=> phone_call_3 

~ phone_call_3

Mom: Hi, Scott. Just wanted to check in.
Scott: Sure, I’m doing okay.
Mom: Great! I had such an awful day running errands today.
do emit("showMouseSignal")
- Scott: Oh yeah?
- Scott: ...
do emit("hideMouseSignal")
Mom: There was a car that almost smashed into me on Rose St. You know where that hardware store just opened up?
do emit("showMouseSignal")
- Scott: Mom, I haven't been home in 10 years. [if sanity <= 71]
- Scott: Nope. 
- Scott: Yep.
do emit("hideMouseSignal") 
Mom: There's no left turn there, but this guy just didn't care and almost hit me.
Mom: I swear, old people can't drive any more.
Mom: You know, eventually I'm going to be too old to drive.
Mom: I'll have to hire a driver! Can you imagine? 
do emit("showMouseSignal")
- Scott: Couldn't dad just drive you around?
- Scott: I definitely can.
do emit("hideMouseSignal")
if missed_calls >= 0:
	Mom: Make sure you pick up when I call. Okay?
	Mom: I get worried when you don't.
	Scott: I know.
Mom: Well, it's late here. You know the time zones make it hard to call you sometimes.
Mom: But I wouldn't miss it for a day.
Mom: I love you. Bye!
=> post_phone_call_3


~ post_phone_call_3
# lowest possible sanity here is 60
Eventually, Gambit headbutts him enough that he gets up and makes him dinner.
Scott: Okay, okay.
Scott's more tired than he expected, but he finishes his nightly routine. 


~ day_3_bed
do emit("goToBed")
Scott curls up in bed, and scrolls on his phone for a while.
Gambit yowls and tosses around a toy for a few minutes before lying on top of his feet.
if sanity <=80:
	He tries to recall the events of the day. 
	Grey clouds. 
	The train.
	Work.
	The train.
	Dinner.
	Television.
	The phone.
Soon, he's asleep.

# => day_x+1_house_am

#=> snippets/day_2_train_am

do day += 1
do time = TIMEOFDAY.MORNING
do emit("toHouseAM")