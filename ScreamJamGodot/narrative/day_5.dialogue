# import "res://narrative/day_2.dialogue" as snippets

~ day_5_house_am
if sanity >= 90:
	Scott has never seen weather like this in his years in San Francisco. 
	He’s so used to the fog that it almost chills him to the bone to see the sun, so many days in a row. He enjoys it, of course, as does Gambit. 
	Gambit sits in a little sun puddle, enjoying the morning.
	do emit("turnOffAlarm")
	Scott does his normal morning routine. He's almost out of shower gel, so he writes that on his grocery list.
elif sanity >=25:
	He almost doesn't want to go to work today. Everything this week has felt difficult.
	do emit("turnOffAlarm")
	He showers, but upon closer inspection, he sees a break out starting on his chin.
	Scott decides not to shave. 
else:
	Scott: There's somewhere to go.
	Scott: Something I was supposed to do.
	Something in the house is missing, but I can't remember what.
	do emit("turnOffAlarm")
	


do emit("showCoffee")

if sanity >= 90:
	As he steps into the crowded cafe, the barista hands him a coffee.
	Scott: One cream?
	Barista: Of course, Scott! Have a good day.
	He smiles back at her, and waves as he walks away.
elif sanity >=25:
	As he steps into the crowded cafe, the barista hands him a coffee.
	Scott: Thanks!
	He smiles back at her, and waves as he walks away.
else:
	He's trying to remember what kind of coffee he drinks, when he steps into the cafe.
	Barista: Hi! What can I get you?
	Scott: What would you suggest?
	Barista: How about our house blend?
	Scott: Do you think I'd like that?
	Barista: Would you like to try it?
	do emit("showMouseSignal")
	- Scott: Okay! Can you put in one cream?
	do emit("hideMouseSignal")
	Barista: Sure!
	Barista: Have a great day!
do emit("toTrainAM")
=> END!


~ day_5_train_am
if sanity <= 25:
	Scott: I'm pretty sure this is the way. 
	The inside of his mouth hurts but he can't stop chewing his inner lips.
	He sits on the train for what feels like forever.
	Don't leave. I think you should stay here longer.
else:
	He stares at his phone, scrolling through the news and social media. He drinks his coffee and finishes it as he’s getting off the train.

do emit("toOffice")
=> END!

~ day_5_office
if sanity <= 25:
	Scott: What am I supposed to be doing today?
	What kind of job did he have?
	He does it, for a while.
else:
	Dylan, Alexis and Paul start arguing over something silly about their favorite movie. 
	Scott keeps his head down and works.
	Dylan: Scott?
	do emit("jumpScareSound")
	[wait=1] ! [wait=1]
	Scott: Yeah?
	Dylan: Want to go out to the bar with us tonight?
	Scott: Nah, I gotta get home.
	Alexis: But it's Friday night!
	Scott smiles.
	He finishes ordering his Black and Bleu salad, eats it as normal, and finishes up his work.

do time = TIMEOFDAY.NIGHT	
do emit("toTrainPM")
=> END!


~ day_5_train_pm
if sanity <= 25:
	He goes home, by reflex, without thinking about it.
else:
	He has to stand due to rush hour traffic on the train, but he faces the direction the train is going and gets a good look out the window.
do emit("toHousePM")
=> END!

~ day_5_house_pm

if sanity <= 25:
	He cooks himself dinner from ??? that he finds in the fridge. 
	Luckily, these are completely precooked, so he doesn’t have to do anything except fend off [shake rate=20 level=10]????[/shake] from eating the bits of chicken he doesn’t get to quickly.
	Scott: This seems okay.
else:
	He cooks himself dinner from the premade meals package he gets delivered. He pretends he’s ordering for four people, to get the discount, but it means he eats the same meals over and over. 
	Luckily, these are completely precooked, so he doesn’t have to do anything except fend off Gambit from eating the bits of chicken he doesn’t get to quickly.
	He flips through Youtube videos on chess and history that are catered to him algorithmically. 

do emit("showCall")
=> END!

~ deny_call_mom
if sanity >= 90:
	Scott: Pick up the phone.
	Scott: Come on, Scott, you've already done everything perfectly this week.
	Scott: [shake rate=20 level=10](Why would you mess it up now?)[/shake]
	=> pick_up_call_mom
else:
	if missed_calls == 3:
		Scott: [shake rate=20 level=10](Why would I pick up now?)[/shake]
		Scott: I've never told this story, but I might as well now.
		Scott: Actually, I don't think I can.
		Scott: I was thinking about this thing that happened to me when I was in school.
		Scott: But honestly, now that I'm trying to remember it, I can't.
		Scott: I could make something up instead. But I don't think that'll benefit anyone.
	else:
		Scott: It's slipping away. Thankfully.
	No one leaves a voicemail.
=> post_phone_call_5

~ pick_up_call_mom
Scott: Hi, mom.
=> phone_call_5 

~ phone_call_5
if sanity >= 90:
	Mom: Hi, Scott. Just wanted to check in.
	Scott: Sure, I’m doing okay.
	Mom: Great! Did you see that Martin Remowitz is running for president?
	do emit("showMouseSignal")
	- Scott: Really?
	- Scott: That's wild!
	do emit("hideMouseSignal")
	Mom: I know! With the scandals he's seen as a liberal actor, I'd never think he'd go that far.
	do emit("showMouseSignal")
	- Scott: Uh huh...
	- Scott: Yep.
	do emit("hideMouseSignal")
	Mom: How's the weather there?
	do emit("showMouseSignal")
	- Scott: What?
		do emit("hideMouseSignal")
		Mom: What do you mean what?
		do emit("showMouseSignal")
		- Scott: You can hear me?
			do emit("hideMouseSignal")
			[i](She laughs at me. She's laughing again...][/i]
			[i][shake rate=20 level=10](it hurts)[/shake][/i]
			Mom: Of course I can hear you!
		- Scott: You don't usually ask me anything...
			do emit("hideMouseSignal")
			[i](She sighs, exasperatedly at me.)[/i]
			[i][shake rate=20 level=10](i hate it)[/shake][/i]
			Mom: Oh my god. Don't be that way.
		Mom: I call you every day! 
		Mom: I obviously care about you.
		Mom: Anyway, answer my question.
	- Scott: It's fine.
		do emit("hideMouseSignal")
		Mom: That's great!
	Mom: It's been raining on and off here for a while.
	Scott: I'm not sure why, but it's been so clear here.
	Mom: Oh, I know why!
	Scott: You do?
	Mom: Yeah.
	Mom: You're in the eye of the hurricane.
	Mom: Well, I have to go. Your dad wants me to make him dinner.
	Mom: I love you. Bye!
elif sanity >=25:
	Mom: Scott, are you okay?
	do emit("showMouseSignal")
	- Scott: No.
	- Scott: Yeah, why?
	do emit("hideMouseSignal")
	Mom: You haven't been picking up the phone when I call you...
	do emit("showMouseSignal")
	- Scott: Yes, I have.
	- Scott: I'm sorry.
	do emit("hideMouseSignal")
	Mom: It makes me really worry about you. 
	Mom: You live so far from me. And I miss you so much.
	do emit("showMouseSignal")
	- Scott: Mom, do you even know me? [if sanity <= 41]
	- Scott: Mom, you haven't seen me in 10 years. [if sanity <= 51]
	- Scott: Mom, I miss you too. [if sanity <= 71]
	- Scott: Yeah, I know.
	do emit("hideMouseSignal")
	Mom: I just... want to make sure you're okay.
	Mom: Anyway, do you remember Joanie? She was in your grade in high school. Her mom lives right by us.
	do emit("showMouseSignal")
	- Scott: Not at all.
	- Scott: Yep.
	do emit("hideMouseSignal")
	Mom: It turns out she's friends with this guy who is going to jail because he was selling drugs.
	Mom: Did you hear about that?
	do emit("showMouseSignal")
	- Scott: No.
	- Scott: Yes.
	do emit("catMeow")
	do emit("hideMouseSignal")
	Mom: It was on the news today!
	Mom: Well, anyway, I gotta go.
	Mom: I miss you so much.
	Scott: I'm sure you do.
	Mom: I love you. Bye!
else:
	...
	Scott: Hello? Who is this? Are you there?
	Scott: [shake rate=20 level=10]There's no one there?[/shake]
	Scott looks down at his phone.
	Scott: Whatever.
=> post_phone_call_5


~ post_phone_call_5

It's time for bed.

~ day_5_bed
do emit("goToBed")
if sanity >= 90:
	Scott stares out at the clear sky. 
	The buildings stretch for what feels like miles out the window.
	He plugs in his phone to charge. 
	Gambit jumps up and hides in the crook of his legs.
	As he's drifting off to sleep, he smells, softly, from what feels like years ago, his mom's hair.
	Ending 3 of 3
	do emit("endGame") 
elif sanity >=25:
	The sky, slick with fog, has left Scott somewhere in what feels like purgatory.
	It's no use\: he's going through the motions.
	Living life like this won't help him move on. 
	In the dark, the changing light from his phone flickers around him.
	And then, he falls asleep.
	Ending 2 of 3
	do emit("endGame") 
else:
	In the dark, in the fog, Scott breathes softly.
	The small twinges of pain he's felt through out the week feels like little scratches across his body.
	Hundreds of little cuts.
	He went somewhere this week.
	Some one was supposed to call, him, maybe, tonight?
	Something was supposed to be with him here.
	As he drifts off, he is left with nothing.
	Ending 1 of 3
	do emit("endGame") 
	
# => day_x+1_house_am

#=> snippets/day_2_train_am


# ENDING